#version 450

// --- Macros ---
#define HitInfo0 HitInfo( false, 0.0, vec3(0), vec3(0), Material(vec4(0), vec4(0), vec4(0), 0.0) )

// --- Constants ---
const float PI = radians(180);
const bool  CULL_FACE = true;
const bool  CLIP_MESHES = false; // Disable until triangle raycasting becomes more expensive
const int   RAY_SUBDIVISIONS = 20;
const float RAY_STEP_RANDOMNESS = 0.025;

const float kEpsilion = 0.001; // Rename to K_EPSILION?

// --- Structs ---
// RTMaterial
struct Material {
    vec4 color;
    vec4 emissionColor;
    vec4 specularColor;
    float smoothness;
};

// RTSphere
struct Sphere {
    float radius;
    vec3 center;
    Material material;
};

// RTTriangle
struct Triangle {
    vec3        p0,
                p1,
                p2,
                normal0,
                normal1,
                normal2;
    Material    material;
};

// RTMeshInfo
struct MeshInfo {
    uint    startIndex,
            count;
    vec3    boundingBoxMin,
            boundingBoxMax;
};

// RTBlackhole
struct Blackhole {
    float   radius;
    vec3    center;
};

// Hit information
struct HitInfo {
    bool        didHit;
    float       dist;
    vec3        pos;
    vec3        normal;
    Material    material;
};

// Ray
struct Ray {
    vec3 origin;
    vec3 dir;
    bool destroyed;
};

// --- Input/Output ---
layout (push_constant) uniform FrameUBO {
    vec3    cameraPos;
    mat4    localToWorld;
    int     frameNumber;
} frame;

// UBO input parameters
layout (binding = 0) uniform ParameterUBO {
    // Camera
    vec2        screenSize;
    float       fov,
                focusDistance;

    // Raytracing settings
    uint            maxBounces,
                    raysPerFrag;
    float           divergeStrength,
                    blackholePower;

    // Other
    uint            spheresCount,
                    blackholesCount;
} ubo;

// Spheres in and out
layout(std140, binding = 1) readonly buffer SphereSSBOIn {
   Sphere spheresIn[ ];
};

layout(std140, binding = 2) readonly buffer BlackholeSSBOIn {
   Blackhole blackholesIn [ ];
};

// Output storage image
layout (binding = 3, rgba8) writeonly uniform image2D image;

// --- Randomness functions ---

// www.pcg-random.org, www.shadertoy.com/view/XlGcRh
/**
 * Generates a psuedo-random unsigned integer with value [0, 2^32 - 1].
 *
 * @param seed The seed, which is changed after use.
 * @return A psuedo-random unsigned integer.
 */
uint randInt(inout uint seed) {
    seed = seed * 747796405 + 2891336453;
    uint result = ((seed >> ((seed >> 28) + 4)) ^ seed) * 277803737;
    result = (result >> 22) ^ result;
    return result;
}

/**
 * Generates a psuedo-float with value [0, 1].
 *
 * @param seed The seed, which is changed after use. 
 * @return A psuedo-random float.
 */
float randFloat(inout uint seed) {
    return randInt(seed) / 4294967295.0; // 2^32 - 1
}

// https://stackoverflow.com/a/6178290
/**
 * Generates a normal-distributed psuedo-random float.
 *
 * @param seed The seed, which is changed after use. 
 * @return A normal-distributed psuedo-random float.
 */
float randFloatNormDist(inout uint seed) {
    float theta = 2 * PI * randFloat(seed);
    float rho = sqrt(abs(-2 * log(randFloat(seed))));
    return rho * cos(theta);
}

/**
 * Generates a normal-distributed psuedo-random 2D vector.
 *
 * @param seed The seed, which is changed after use. 
 * @return A normal-distributed psuedo-random vec2 for use in polar spaces.
 */
vec3 randVecNormDist(inout uint seed) {
    float x = randFloatNormDist(seed),
            y = randFloatNormDist(seed),
            z = randFloatNormDist(seed);
    return normalize(vec3(x, y, z));	
}

/**
 * Geneates a normal-distributed psuedo-random 2D vector.
 * While randVecNormDist() generates a normal-distribution for polar coordinates, this function does so for a square (cartesian space). 
 *
 * @param seed The seed, which is changed after use. 
 * @return A normal-distributed psuedo-random vec2 for use in cartesian spaces.
 */
vec2 randVecCartesianNormDist(inout uint seed) {
    float ang = randFloat(seed) * 2 * PI;
    vec2 pos = vec2(cos(ang), sin(ang));
    return pos * sqrt(abs(randFloatNormDist(seed))); // Normal distribution
}

// --- Environment functions ---
/**
 * Gets the environment light where a ray goes.
 *
 * @param ray The ray.
 * @return The environment light for the ray. 
 */
vec3 GetEnvironmentLight(Ray ray) {
    vec3 sunPosition = vec3(0,1,0);

    // Set up environment
    // TODO: Move these to another place.
    vec3 	SkyColourHorizon = vec3(1,0,1),
            SkyColourZenith = vec3(0.5,0.2,1),
            GroundColour = vec3(0.1,0.2,0.2);
    float   SunFocus = 10.f,
            SunIntensity = 0.5f;
    
    // Calculate gradients
    float skyGradientT = pow(smoothstep(0.0, 0.4, ray.dir.y), 0.35);
    float groundToSkyT = smoothstep(-0.01, 0.0, ray.dir.y);
    vec3 skyGradient = mix(SkyColourHorizon, SkyColourZenith, skyGradientT);
    float sun = pow(max(0, dot(ray.dir, sunPosition)), SunFocus) * SunIntensity;

    // Combine ground, sky, and sun, and return the final color
    return mix(GroundColour, skyGradient, groundToSkyT) + sun * int(groundToSkyT>=1);
}

// --- Ray intersection functions ---
/**
 * Checks for an intersection between a ray and a sphere.
 *
 * @param ray The ray.
 * @param sphere The sphere.
 *
 * @return The hit information from the (possible) intersection.
 */
HitInfo RaySphere(Ray ray, Sphere sphere) {	
    HitInfo hitInfo = HitInfo0;
    vec3 offsetRayOrigin = ray.origin - sphere.center;

    // Solve for distance with a quadratic equation
    float a = dot(ray.dir, ray.dir);
    float b = 2 * dot(offsetRayOrigin, ray.dir);
    float c = dot(offsetRayOrigin, offsetRayOrigin) - sphere.radius*sphere.radius;

    // Quadratic discriminant
    float discriminant = b * b - 4 * a * c; 

    // If d > 0, the ray intersects the sphere => calculate hitinfo
    if (discriminant >= 0) {
        float dist = (-b - sqrt(abs(discriminant))) / (2 * a);

        // (If the intersection happens behind the ray, ignore it)
        if (dist >= 0) {
            hitInfo.didHit = true;
            hitInfo.dist = dist;
            hitInfo.pos = ray.origin + ray.dir * dist;
            hitInfo.normal = normalize(hitInfo.pos - sphere.center);
        }
    }

    // Otherwise, ray does not intersect sphere => return blank hitinfo
    return hitInfo;
}

/**
 * Checks for an intersection between a ray and a triangle.
 * Uses the Möller-Trumbore algorithm, see:
 * https://en.wikipedia.org/wiki/M%C3%B6ller%E2%80%93Trumbore_intersection_algorithm
 *
 * @param ray The ray.
 * @param triangle The triangle.
 *
 * @return The hit information from the (possible) intersection.
 */
HitInfo RayTriangle(Ray ray, Triangle triangle) {
    HitInfo hitInfo = HitInfo0;

    // Define vectors
    vec3    v0 = triangle.p1 - triangle.p0,
            v1 = triangle.p2 - triangle.p0,
            v0v1c = cross( v0, v1 );
    
    // Define determinant and inverse determinant
    float   determinant = -dot( ray.dir, v0v1c ),
            invDeterminant = 1.0 / determinant;

    // If culling is enabled, verify that ray passes through triangle the right direction
    if ( CULL_FACE && determinant < kEpsilion )
        return hitInfo;
    
    // (Check if ray is parallel with triangle)
    else if ( abs(determinant) < kEpsilion )
        return hitInfo;

    // Calculate distance to triangle and barycentric coordinates
    vec3    v3 = ray.origin - triangle.p0,
            v3dirc = cross( v3, ray.dir );

    float   dist = dot( v3, v0v1c ) * invDeterminant,
            u = dot( v1, v3dirc ) * invDeterminant, 
            v = -dot( v0, v3dirc ) * invDeterminant,
            w = 1.0 - u - v;
    
    // Calculate intersection information and return
    hitInfo.didHit  = dist >= 0.0 && u >= 0.0 && v >= 0.0 && w >= 0.0;
    hitInfo.dist    = dist;
    hitInfo.pos     = ray.origin + ray.dir * dist;
    hitInfo.normal  = normalize( triangle.normal0 * w + triangle.normal1 * u + triangle.normal2 * v );

    return hitInfo;
}

/**
 * Checks for an intersection between a ray and a bounding box.
 * Thanks to:   https://gist.github.com/DomNomNom/46bb1ce47f68d255fd5d
 *              https://alain.xyz/blog/ray-tracing-acceleration-structures
 *
 * @param ray The ray.
 * @param boxMin The bottom left corner of the box.
 * @param boxMax The top right corner of the box.
 *
 * @return If the ray intersects the box at all.
 */
bool RayBoundingBox(Ray ray, vec3 boxMin, vec3 boxMax) {
    vec3    rayDirInverted = 1.0 / ray.dir,
            boxMinRelative = (boxMin - ray.origin) * rayDirInverted,
            boxMaxRelative = (boxMax - ray.origin) * rayDirInverted,
            boxMinNew = min( boxMinRelative, boxMaxRelative ),
            boxMaxNew = max( boxMinRelative, boxMaxRelative );
    
    float   maxMinAxis = max( max( boxMinNew.x, boxMinNew.y ), boxMinNew.z ),
            minMaxAxis = min( min( boxMaxNew.x, boxMaxNew.y ), boxMaxNew.z );

    return maxMinAxis <= minMaxAxis;
}

// --- Raytracing functions ---
HitInfo CalculateRayCollision(Ray ray, float stepDist) {
    HitInfo closestHit = HitInfo0;
    closestHit.dist = -1;

    // Raycast spheres
    for (int i = 0; i < ubo.spheresCount; i++) {
        Sphere sphere = spheresIn[i];

        HitInfo hitInfo = RaySphere(ray, sphere);
        if (hitInfo.didHit && hitInfo.dist <= stepDist && ( closestHit.dist < 0 || hitInfo.dist < closestHit.dist ) )
        {
            closestHit = hitInfo;
            closestHit.material = sphere.material;
        }
    }

    // Return the collision which occured closest to the origin
    return closestHit;
}

void SampleLineSegment(inout Ray ray, inout float stepDist, inout vec3 incomingLight, inout vec3 rayColor, inout uint seed) {
    while ( stepDist > 0.f ) {
        // Check for ray intersection between current position and predicted
        HitInfo hitInfo = CalculateRayCollision(ray, stepDist);
        if ( hitInfo.didHit ) {
            // Update stepdist and ray
            stepDist -= hitInfo.dist;
            ray.origin = hitInfo.pos;
            Material material = hitInfo.material;

            bool 	isSpecular  = material.specularColor.w >= randFloat(seed);
            vec3 	specularDir = reflect(ray.dir, hitInfo.normal),
                    diffuseDir  = normalize(hitInfo.normal + randVecNormDist(seed));
            ray.dir = normalize(mix(diffuseDir, specularDir, material.smoothness * int(isSpecular)));

            // Sample
            vec3 emittedLight = material.emissionColor.xyz * material.emissionColor.w;
            incomingLight += emittedLight * rayColor;
            rayColor *= mix(material.color, material.specularColor, int(isSpecular)).rgb;

            // Early exit if ray color ~= 0
            // (Use some randomness to avoid "artificial" look)
            float p = max(rayColor.r, max(rayColor.g, rayColor.b));
            if (randFloat(seed) >= p) {
                ray.destroyed = true;
                return;
            }
            rayColor *= 1.0f / p;
        } else {
            ray.origin += ray.dir * stepDist;
            stepDist -= stepDist;
        }
    }
}

/**
 *  Bends the light ray's direction and outputs the predicted step distance.
 */
float BendLight(inout Ray ray, inout uint seed) {
    for (int i = 0; i < ubo.blackholesCount; i++) {
        Blackhole blackhole = blackholesIn[i];

        // Get direction and distance to the black hole
        vec3    dirToHole = blackhole.center - ray.origin;
        float   dist = length( dirToHole ); dirToHole /= dist;
        float   invDist = 1.f / dist;

        // Destroy ray and return if it's too close to the black hole
        if (dist < blackhole.radius) {
            ray.destroyed = true;
            return -1.f;
        }

        // Calculate forces
        float   invDistSqr = invDist * invDist;
        float   bendForce  = invDistSqr * ubo.blackholePower;
        float   spinForce  = invDistSqr * 0.f; // TODO: Add spin as a black hole property
        
        // Calculate step distance
        // (For now, this assumes only ONE black hole exists.)
        float   randomFactor = mix( 1.0-RAY_STEP_RANDOMNESS, 1.0/(1.0-RAY_STEP_RANDOMNESS), randFloat(seed) );
        float   distFactor = 0.5f * dist;
        float   stepDist = 0.1f + randomFactor * distFactor;

        // Change direction of lightray
        ray.dir = normalize( ray.dir + dirToHole * bendForce * stepDist + cross( dirToHole, vec3(0,0,1) * spinForce * stepDist ) );

        // FOR NOW; ONLY WORKS WITH ONE BLACK HOLE
        return stepDist;
    }
}

vec3 Trace(Ray ray, inout uint seed) {
    vec3 	incomingLight = vec3(0),
            rayColor = vec3(1);
    
    int rayDivision = 0;
    while (rayDivision < RAY_SUBDIVISIONS) {
        rayDivision++;

        // Create line segment from the current ray position to the predicted next one
        float stepDist = BendLight(ray, seed);
        if (ray.destroyed) break;
        SampleLineSegment(ray, stepDist, incomingLight, rayColor, seed);
        if (ray.destroyed) break;
    }

    // If the ray was not destroyed but instead went out into space, sample enironment color
    if (ray.destroyed) return vec3(0);
    return incomingLight + GetEnvironmentLight(ray) * rayColor * 0.3f;
}

// --- Program ---
layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
void main()  {
    // Create seed for RNG
    vec2 uv = vec2( gl_GlobalInvocationID.x / ubo.screenSize.x, 1 - gl_GlobalInvocationID.y / ubo.screenSize.y );
    uint i = uint( gl_GlobalInvocationID.y * ubo.screenSize.x + gl_GlobalInvocationID.x );
    uint seed = i + frame.frameNumber * 719393;

    // Calculate focus point
    float   planeHeight = ubo.focusDistance * tan(ubo.fov * 0.5 * PI / 180.0) * 2.0,
            planeWidth = planeHeight * (ubo.screenSize.x / ubo.screenSize.y);
    vec3    viewParams = vec3( planeWidth, planeHeight, ubo.focusDistance );

    vec3    focusPointLocal = vec3(uv - 0.5, 1) * viewParams,
            focusPoint = (frame.localToWorld * vec4(focusPointLocal, 1)).xyz,
            camUp = normalize(frame.localToWorld[1].xyz),
            camRight = normalize(frame.localToWorld[0].xyz);

    // Fire rays
    Ray ray;
    ray.destroyed = false;
    vec3 totalIncomingLight = vec3(0);

    for ( int i = 0; i < ubo.raysPerFrag; i++ )
    {
        // Calculate ray origin and dir
        vec2 jitter = randVecCartesianNormDist(seed) * ubo.divergeStrength / ubo.screenSize.x;
        vec3 focusPointJittered = focusPoint + camRight*jitter.x + camUp*jitter.y;

        // Trace rays
        ray.origin = frame.cameraPos;
        ray.dir = normalize(focusPointJittered - ray.origin);
        totalIncomingLight += Trace(ray, seed);
    }
    
    // Return final color (average of the frag's rays)
    vec3 fragCol = totalIncomingLight / ubo.raysPerFrag;
    imageStore(image, ivec2(gl_GlobalInvocationID.xy), vec4( fragCol, 1 ));
}